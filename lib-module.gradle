apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

android {
  compileSdkVersion Integer.parseInt(project.targetSdk)
  defaultConfig {
    minSdkVersion Integer.parseInt(project.minSdk)
    targetSdkVersion Integer.parseInt(project.targetSdk)
    versionName = project.version
    consumerProguardFiles 'proguard-rules.pro'
  }

  testOptions {
    unitTests.returnDefaultValues = true
  }

  compileOptions {
    sourceCompatibility 1.8
    targetCompatibility 1.8
  }
}

dependencies {
  implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8")

  testImplementation('junit:junit:4.13')
  testImplementation("com.google.truth:truth:1.0")
}

task sourceJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  archiveClassifier.set("sources")
}

afterEvaluate {
  publishing {
    publications {
      android(MavenPublication) {
        from components.release
        artifact(sourceJar)
        pom {
          description = project.description
          url = 'https://github.com/trevjonez/PolyAdapter-Android'

          licenses {
            license {
              name = 'The Apache License, Version 2.0'
              url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
          }

          developers {
            developer {
              id = 'TrevJonez'
              name = 'Trevor Jones'
              email = 'trevorjones141@gmail.com'
            }
          }

          scm {
            connection = 'scm:git:git@github.com:trevjonez/PolyAdapter-Android.git'
            developerConnection = 'scm:git:git@github.com:trevjonez/PolyAdapter-Android.git'
            url = 'https://github.com/trevjonez/PolyAdapter-Android'
          }
        }
      }
    }
  }
}

